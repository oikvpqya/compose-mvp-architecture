import org.jetbrains.compose.desktop.application.dsl.TargetFormat

plugins {
    alias libs.plugins.android.application
    alias libs.plugins.compose.compiler
    alias libs.plugins.google.ksp
    alias libs.plugins.jetbrains.compose
    alias libs.plugins.kotlin.multiplatform
    alias libs.plugins.kotlin.serialization
}

kotlin {
    jvmToolchain(17)
    androidTarget()
    jvm("desktop")

    sourceSets {
        commonMain {
            dependencies {
                implementation compose.material
                implementation compose.ui
                implementation libs.jetbrains.navigation.compose
                implementation libs.kotlinx.coroutines.core
                implementation libs.kotlinx.serialization.json
                implementation libs.ktor.client.core
                implementation libs.takahirom.rin
                implementation libs.tatarka.inject.runtime
            }
        }

        androidMain {
            dependencies {
                implementation libs.androidx.core.ktx
                implementation libs.androidx.activity.compose
                implementation libs.ktor.client.okhttp
            }
        }

        desktopMain {
            dependencies {
                implementation compose.desktop.currentOs
                implementation libs.ktor.client.okhttp
            }
        }

        commonTest {
            dependencies {
                implementation libs.cash.molecule
                implementation libs.cash.turbine
                implementation libs.jetbrains.lifecycle.runtime.compose
                implementation libs.jetbrains.lifecycle.viewmodel.compose
                implementation libs.kotlin.test
                implementation libs.kotlinx.coroutines.test
            }
        }
    }
}

android {
    compileSdk 34
    namespace "me.oikvpqya.playground"

    defaultConfig {
        applicationId "me.oikvpqya.playground"
        minSdk 21
        targetSdk 34
        versionCode 1
        versionName "1.0.0"
    }

    buildTypes {
        release {
            minifyEnabled false
            shrinkResources false
            signingConfig signingConfigs.getByName("debug")
        }
    }

    buildFeatures {
        compose true
    }
}

compose {
    desktop {
        application {
            mainClass = "MainKt"

            nativeDistributions {
                targetFormats(TargetFormat.Dmg, TargetFormat.Msi, TargetFormat.Deb)
                packageName = "me.oikvpqya.playground"
                packageVersion = "1.0.0"
            }
        }
    }
}

dependencies {
    debugImplementation compose.uiTooling
    "androidMainImplementation" compose.preview
    kspAndroid libs.tatarka.inject.compiler
    kspDesktop libs.tatarka.inject.compiler
}
